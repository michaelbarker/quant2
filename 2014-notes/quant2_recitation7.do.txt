    /*******************************************************************************
  Recitation 7: Binary Dependent Variables 
 
 SWBAT: 
 - Run LPM/Probit/Logit commands 
 - Use postestimation commands for above 

*******************************************************************************/

clear
* Note: update path to reflect actual file location.
use "C:\Documents\...\card.dta"

* Today we will talk about regression models that you can use with 
* binary (0/1) dependent variables.

* Look at the relationship between education and whether or not a person had
* a library card at age 14.

* We want to answer the following question: Given the number of year's of education
* someone has, what is the proability that he or she had a library card at age 14?

* Binary dependent variable
tab libcrd14

* Look at education variable
tab educ


* Linear Probability Model just uses regress
reg libcrd14 educ , robust
* Now calculate predicted probabilities with the predict postestimation command:
help regress postestimation
* We want linear predication, the xb option
predict lpm_libcrd14, xb
* Label and graph new variable
label variable lpm_libcrd14 "LPM"
twoway line lpm_libcrd14 educ, name(LPM, replace)


* Probit command is very similar to regress
help probit
* You can also find full examples and a review of probit in the pdf-manual entry
probit libcrd14 educ
* Again, calculate predicted probabilities. For probit, use the pr option of predict
help probit postestimation
predict probit_libcrd14, pr

* Label and graph new variable
label variable probit_libcrd14 "Probit"
twoway line probit_libcrd14 educ, name(Probit, replace)

* Remember, if we make a connected line graph, the points are connected in 
* whichever order they appear in the data set.

* To get a nice graph, sort the data by the X-variable before graphing
sort educ
twoway line probit_libcrd14 educ, name(Probit, replace)


* Logit command is also very similar:
help logit
logit libcrd14 educ
* To get predicted values, use predict with the pr option, just like probit
help logit postestimation
predict logit_libcrd14, pr

* Label and graph new variable
label variable logit_libcrd14 "Logit"
* Note, we already sorted the data by education, so we don't have to sort it again.
twoway line logit_libcrd14 educ, name(Logit, replace)


* Now graph all predicted values together
twoway line lpm_libcrd14 probit_libcrd14 logit_libcrd14 educ 


* Add horizontal lines at predicted probabilities of zero and one
twoway line lpm_libcrd14 probit_libcrd14 logit_libcrd14 educ , yline(0) yline(1)
* Notice the logit and probit predicted values will never cross the 0 or 1 line.
* , unlike predictions from the Linear Probability Model.


